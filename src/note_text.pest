text_mode = _{SOI ~ statement*  ~  EOI}
statement = _{StyleStatement|LineStatement|MathStatement|CodeStatement|SPACE_SEPARATOR|NEWLINE|Escaped|Command|English|Word}
/*====================================================================================================================*/
MathStatement = _{Math|MathRest}
///#E06C75
Math = {MathLevel ~ MathText ~ POP ~ Dollar}
MathLevel = {Dollar~PUSH (Dollar*)}
MathText = {(Escape~Dollar|!(Dollar ~ PEEK) ~ ANY)*}
///Gray
MathRest = {Dollar+}
Dollar = _{"$"}
/*====================================================================================================================*/
CodeStatement = _{Code|CodeRest}
///#98C379
Code = {CodeLevel ~ CodeText ~ POP ~ Accent}
CodeLevel = {Accent~PUSH (Accent*)}
CodeText = {(Escape~Accent|!(Accent ~ PEEK) ~ ANY)*}
///Gray
CodeRest = {Accent+}
Accent = _{"`"}
/*====================================================================================================================*/
StyleStatement = _{Style|StyleRest}
///#D19A66
Style = {StyleLevel ~ SuperText ~ POP ~ Asterisk}
StyleLevel = {Asterisk~PUSH (Asterisk*)}
SuperText = {(Escape~Asterisk|!(Asterisk ~ PEEK) ~ ANY)*}
///Gray
StyleRest = {Asterisk+}
Asterisk = _{"*"}
/*====================================================================================================================*/
LineStatement = _{Line|LineRest}
///#61AFEF
Line = {LineLevel ~ LineText ~ POP ~ Equal}
LineLevel = {Equal~PUSH (Equal*)}
LineText = {(Escape~Equal|!(Equal ~ PEEK) ~ ANY)*}
///Gray
LineRest = {Equal+}
Equal = _{"="}
/*====================================================================================================================*/
//!#C678DD: Function|Escaped
Command = {Function~arguments*}
Function = @{Escape ~ ASCII_ALPHA+}
arguments = _{"{" ~(String|!"}"~ANY)*~ "}"}
///#98C379
String = ${StringSingle|LiteralString}
StringSingle = ${S2  ~  (!S2 ~  ANY)*  ~  S2 }
LiteralString = ${S1  ~  (!S1 ~  ANY)*  ~  S1 }
S1 = _{"'"}
S2 = _{"\""}
/*====================================================================================================================*/
Word = {(!(Escape|English)~SYMBOL)+|Escape}
English = {(ASCII_ALPHA|ASCII_DIGIT)+|PUNCTUATION}
Escaped = @{Escape~(Escape|Equal|Asterisk|Dollar|Accent)}
PUNCTUATION = @{","|"."|"?"|"!"}
SYMBOL = _{!NonCharacter~ANY}
/*====================================================================================================================*/
Escape = _{"\\"}
NonCharacter = _{Asterisk|Equal|Dollar|Accent|SPACE_SEPARATOR|NEWLINE}
/*====================================================================================================================*/
SPACE_SEPARATOR = {" "|"\t"}
NEWLINE = @{CR ~ LF|CR|LF}
CR = @{"\r"}
LF = @{"\n"}