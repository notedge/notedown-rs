text_mode = _{SOI ~ statement*  ~  EOI}
statement = _{Style|Mark|Math|Code|SPACE_SEPARATOR|NEWLINE|Escaped|Order|English|Word}
/*====================================================================================================================*/
Math = _{MathEmpty|MathDisplay|MathInline|MathRest} // `MathEmpty` can be removed but it will slow down
///#C678DD
MathDisplay = {Dollar{2} ~ MathDisplayText ~ Dollar{2}}
MathDisplayText = {(Escape ~ Dollar|!Dollar{2}~ANY)+}
///#D19A66
MathInline = {Dollar ~ MathInlineText ~ Dollar}
MathInlineText = {(Escape ~ Dollar|!Dollar~ANY)+}
//!Gray: MathEmpty|MathRest
MathEmpty = {Dollar{3,}}
MathRest = {Dollar+}
Dollar = _{"$"}
/*====================================================================================================================*/
Code = _{CodeEmpty|CodeDisplay|CodeInline|CodeRest}
///#C678DD
CodeDisplay = {Accent{2} ~ CodeDisplayText ~ Accent{2}}
CodeDisplayText = {(Escape ~ Accent|!Accent{2}~ANY)+}
///#D19A66
CodeInline = {Accent ~ CodeInlineText ~ Accent}
CodeInlineText = {(Escape ~ Accent|!Accent~ANY)+}
//!Gray: CodeEmpty|CodeRest
CodeEmpty = {Accent{3,}}
CodeRest = {Accent+}
Accent = _{"`"}
/*====================================================================================================================*/
Style = _{StyleEmpty|StyleBoldItalic|StyleBold|StyleItalic|StyleRest}
///#61AFEF
StyleItalic = {Asterisk ~ StyleItalicText ~ Asterisk}
StyleItalicText = {(Escape ~ Asterisk|!Asterisk~ANY)+}
///#D19A66
StyleBold = {Bold ~ StyleBoldText ~ Bold}
StyleBoldText = {(Escape ~ Asterisk|!Bold~ANY)+}
///#C678DD
StyleBoldItalic = {BoldItalic ~ StyleBoldItalicText ~ BoldItalic}
StyleBoldItalicText = {(Escape ~ Asterisk|!BoldItalic~ANY)+}
//!Gray: StyleEmpty|StyleRest
StyleEmpty = {Asterisk{4,}}
StyleRest = {Asterisk+}
BoldItalic = _{Asterisk{3}}
Bold = _{Asterisk{2}}
Asterisk = _{"*"}
/*====================================================================================================================*/
Mark = _{MarkEmpty|MarkDelete|MarkUnderline|MarkRest}
///#C678DD
MarkDelete = {Delete ~ MarkDeleteText ~ Delete}
MarkDeleteText = {(Escape ~ Equal|!Delete~ANY)+}
///#D19A66
MarkUnderline = {Equal ~ MarkUnderlineText ~ Equal}
MarkUnderlineText = {(Escape ~ Equal|!Equal~ANY)+}
//!Gray: MarkEmpty|MarkRest
MarkEmpty = {Equal{3,}}
MarkRest = {Equal+}
Delete = _{Equal{2}}
Equal = _{"="}
/*====================================================================================================================*/
Word = {(!(Escape|English)~SYMBOL)+}
English = {(ASCII_ALPHA|ASCII_DIGIT)+|PUNCTUATION}
Order = @{Escape ~ ASCII_ALPHA+}
Escaped = @{Escape~(Escape|Equal|Asterisk|Dollar)}
PUNCTUATION = @{","|"."|"?"}
SYMBOL = _{!NonCharacter~ANY}
/*====================================================================================================================*/
Escape = _{"\\"}
NonCharacter = _{Asterisk|Equal|Dollar|Accent|SPACE_SEPARATOR|NEWLINE}
/*====================================================================================================================*/
SPACE_SEPARATOR = {" "|"\t"}
NEWLINE = @{CR ~ LF|CR|LF}
CR = @{"\r"}
LF = @{"\n"}