program = _{SOI ~ statement*  ~  EOI}

statement = _{
    Header
  | TextBlock
  | Command
  | EmptyLine{1,}
}

EmptyLine = _{WHITESPACE*  ~  NEWLINE}

Command = _{CommandLine|CommandPart| CommandBlock}
/*====================================================================================================================*/
//!#e06c75: Header
Header = ${Sharp{1, 6}  ~  arguments?  ~  RestOfLine}
Sharp = @{"#"}
/*====================================================================================================================*/
TextBlock = {TextHeaderCharacter  ~  TextBlockLine*}
TextHeaderCharacter = _{!(NEWLINE|Escape|Sharp)  ~ ANY}
TextBlockLine = _{!(EmptyLine{2,}|Sharp)  ~ ANY}

/*====================================================================================================================*/
//!#C678DD: Begin|End
CommandPart = ${Begin ~ arguments  ~  CommandContent   ~  End  ~  arguments?}
CommandContent = ${(CommandPart|!End ~ ANY)*}
Begin = @{Escape ~ "begin"}
End = @{Escape ~ "end"}
Comma = @{","}

//!#56B6C2: Set
//!#E06C75: key
CommandBlock = !{command ~  arguments*}
arguments = {LB  ~ (key_value|argument)* ~  RB}
argument = {value  ~  Comma?}
key_value = {key  ~  Set  ~ value  ~  Comma?}
key = {SYMBOL}
value = {SYMBOL|Integer|String}
Set = @{"="}
LB = @{"{"}
RB = @{"}"}


//!#61AFEF: command|SYMBOL
CommandLine = {command  ~  Colon  ~  RestOfLine}
command = @{Escape ~ SYMBOL*}
RestOfLine = !{(!NEWLINE  ~  ANY)*}



///# White


Colon = @{":"}
Escape = @{"\\"}

/*====================================================================================================================*/
//!#D19A66: Integer|Decimal|DecimalBad|Exponent|ComplexHandler
Number = ${Exponent|Complex|Decimal|DecimalBad|Integer}
Decimal = ${Integer  ~ Dot ~  ASCII_DIGIT+}
DecimalBad = ${Integer ~ Dot | Dot ~ ASCII_DIGIT+}
Exponent = ${(Decimal|Integer)  ~ "^^" ~  (Decimal|Integer)}
Complex = ${(Decimal|Integer) ~  ComplexHandler}
Integer = @{Zero|ASCII_DIGIT  ~  (Underline?  ~  ASCII_NONZERO_DIGIT)* }
ComplexHandler = @{SYMBOL}
Dot = @{"."}
Zero = @{"0"}
/*====================================================================================================================*/
//!#98C379: String|MultiLineComment|CommandContent|CommandLine
String = ${StringSingle|StringBlock|LiteralString| LiteralBlock}//TODO: 字符串表达式
StringSingle = ${S2  ~  (!S2 ~  ANY)*  ~  S2 }
StringBlock = ${S6  ~  (!S6 ~  ANY)*  ~  S6 }
LiteralString = ${S1  ~  (!S1 ~  ANY)*  ~  S1 }
LiteralBlock = ${S3  ~  (!S3 ~  ANY)*  ~  S3 }
S1 = _{"'"}
S2 = _{"\""}
S3 = _{"'''"}
S6 = _{"\"\"\""}
/*====================================================================================================================*/
SYMBOL = @{NameStartCharacter ~ NameCharacter*}

NameCharacter = _{ASCII_DIGIT|NameStartCharacter}
NameStartCharacter = _{Underline|ASCII_ALPHA}


Underline = @{"_"}
Asterisk = @{"*"}


/*====================================================================================================================*/
NEWLINE = @{CR ~ LF|CR|LF}
WHITESPACE = _{SPACE_SEPARATOR| TAB}
TAB = @{"\t"}
CR = @{"\r"}
LF = @{"\n"}

//!Gray:BlockComment|LineComment
COMMENT = ${BlockComment|LineComment }
LineComment = ${Comment  ~ WHITESPACE* ~  Colon  ~  RestOfLine}
BlockComment = ${Comment  ~ WHITESPACE* ~  LB  ~ MultiLineComment ~  RB}
MultiLineComment = ${(BlockComment | !RB  ~  ANY)*}
Comment = @{Escape ~ "comment"}
