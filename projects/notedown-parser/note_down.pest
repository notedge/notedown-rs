program = _{SOI ~ statement*  ~  EOI}
TextMode = _{SOI ~ text*  ~  EOI}
statement = _{
    Header
  | HorizontalRule
  | Code
  | Table
  | List
  | Command
  | EmptyLine{1,}
  | TextBlock
}
text = _{StyleStatement|LineStatement|MathStatement|RawStatement|NEWLINE|Escaped|Command|TextRest}
EmptyLine = _{WHITESPACE*  ~  NEWLINE}
/*====================================================================================================================*/
//!#E06C75: Header
Header = ${Sharp{1, 6}  ~  arguments?  ~  RestOfLine}
/*====================================================================================================================*/
TextBlock = {TextHeaderCharacter  ~  TextBlockLine*}
TextHeaderCharacter = _{!NEWLINE  ~ ANY}
TextBlockLine = _{!(EmptyLine{2,}|Sharp)  ~ ANY}
TextRest = {(!(WHITE_SPACE|NEWLINE|Escape|Tilde|Asterisk|Dollar|Accent) ~ ANY)+}
/*====================================================================================================================*/
//!#61AFEF: Code
//!#E06C75: CodeText
Code = {CodeLevel ~ CodeAction? ~SYMBOL? ~ arguments? ~ CodeText ~ POP ~ CodeMark{3}}
CodeAction = {At|Asterisk|Sharp}
CodeLevel = {CodeMark{3}~PUSH(CodeMark*)}
CodeText = {(!(CodeMark ~ PEEK) ~ ANY)*}
CodeMark = @{Accent}
/*====================================================================================================================*/
//!Orange: TableMark
Table = {TableMark ~ (!TableEnd~ANY)* ~ TableEnd}
TableEnd = {EmptyLine{2}|EmptyLine~EOI|EOI}
TableMark = @{Vertical}
/*====================================================================================================================*/
//!Green: ListMark
List = {ListMark ~ (!ListEnd~ANY)* ~ ListEnd}
ListEnd = {EmptyLine{2}|EmptyLine~EOI|EOI}
program_list = _{SOI ~ ListStatement+  ~  EOI}
ListStatement = {NEWLINE~ListHeadLevel~ListMark~ ListRest}
ListHeadLevel = {WHITESPACE*}
ListRest = {(!ListStatement~ANY)*}
ListMark = @{Plus|Minus|Vertical|QuoteMark|Integer~Dot}
/*====================================================================================================================*/
///Red
HorizontalRule = @{Set{3,}|Minus{3,}}
/*====================================================================================================================*/
MathStatement = _{Math|MathRest}
///#E06C75
Math = {MathLevel ~ MathText ~ POP ~ Dollar}
MathLevel = {Dollar~PUSH (Dollar*)}
MathText = {(Escape~Dollar|!(Dollar ~ PEEK) ~ ANY)*}
///Gray
MathRest = {Dollar+}
/*====================================================================================================================*/
RawStatement = _{Raw|RawRest}
///#98C379
Raw = {RawLevel ~ RawText ~ POP ~ Accent}
RawLevel = {Accent~PUSH (Accent*)}
RawText = {(Escape~Accent|!(Accent ~ PEEK) ~ ANY)*}
///Gray
RawRest = {Accent+}
/*====================================================================================================================*/
StyleStatement = _{Style|StyleRest}
///#D19A66
Style = {StyleLevel ~ StyleText ~ POP ~ Asterisk}
StyleLevel = {Asterisk~PUSH (Asterisk*)}
StyleText = {(Escape~Asterisk|!(Asterisk ~ PEEK) ~ ANY)*}
///Gray
StyleRest = {Asterisk+}
/*====================================================================================================================*/
LineStatement = _{Line|LineRest}
///#61AFEF
Line = {LineLevel ~ LineText ~ POP ~ Tilde}
LineLevel = {Tilde~PUSH (Tilde*)}
LineText = {(Escape~Tilde|!(Tilde ~ PEEK) ~ ANY)*}
///Gray
LineRest = {Tilde+}
/*====================================================================================================================*/
Command = _{CommandLine|CommandPart| CommandBlock}
//!#C678DD: Begin|End
CommandPart = ${Begin ~ arguments  ~  CommandContent   ~  End  ~  arguments?}
CommandContent = ${(CommandPart|!End ~ ANY)*}
Begin = @{Escape ~ "begin"}
End = @{Escape ~ "end"}

//!#56B6C2: Set
//!#E06C75: key
CommandBlock = !{command ~  arguments*}
arguments = _{
    "{"  ~ (key_value|argument)* ~  "}"
  | "["  ~ argument_literal ~  "]"
}
argument_literal = {(Escape ~ (Escape|"]")|!"]" ~ ANY)*}
argument = {value  ~  Comma?}
key_value = {key  ~  Set  ~ value  ~  Comma?}
key = {SYMBOL}
value = {SYMBOL|Integer|String}

//!#61AFEF: CommandBlock
CommandLine = {command  ~arguments?~  Colon  ~  RestOfLine}
command = @{Escape ~ SYMBOL}
RestOfLine = !{(!NEWLINE  ~  ANY)*}
/*====================================================================================================================*/
//!#D19A66: Integer|Decimal|DecimalBad|Exponent|ComplexHandler
Number = ${Exponent|Decimal|DecimalBad|Integer}
Decimal = ${Integer  ~ Dot ~  ASCII_DIGIT+}
DecimalBad = ${Integer ~ Dot | Dot ~ ASCII_DIGIT+}
Exponent = ${(Decimal|Integer)  ~ "^^" ~  (Decimal|Integer)}
Integer = @{Zero|ASCII_DIGIT  ~  (Underline?  ~  ASCII_NONZERO_DIGIT)* }
Zero = @{"0"}
/*====================================================================================================================*/
///#98C379
String = ${
    S1  ~  (Escape ~ (Escape|S1)|!S1 ~ ANY)*  ~  S1
  | S2  ~  (Escape ~ (Escape|S2)|!S2 ~ ANY)*  ~  S2
}
S1 = _{"'"}
S2 = _{"\""}
/*====================================================================================================================*/
SYMBOL = @{
    XID_START ~ XID_CONTINUE?
  | Underline~XID_CONTINUE+
}
Escaped = @{Escape~(Escape|Tilde|Asterisk|Dollar|Accent)}
// NameCharacter = _{ASCII_DIGIT|NameStartCharacter}
// NameStartCharacter = _{Underline|ASCII_ALPHA}
/*====================================================================================================================*/
//!Gray:BlockComment|LineComment
COMMENT = ${BlockComment|LineComment }
LineComment = ${Comment  ~ WHITESPACE* ~  Colon  ~  RestOfLine}
BlockComment = ${Comment  ~ WHITESPACE* ~  "{"  ~ MultiLineComment ~  "}"}
MultiLineComment = ${(BlockComment | !"}"  ~  ANY)*}
Comment = @{Escape ~ "comment"}
/*====================================================================================================================*/
NEWLINE = @{CR ~ LF|CR|LF}
WHITESPACE = @{SPACE_SEPARATOR|TAB}
TAB = @{"\t"}
CR = @{"\r"}
LF = @{"\n"}
/*====================================================================================================================*/
Escape = @{"\\"}
At = @{"@"}
Sharp = @{"#"}
Underline = @{"_"}
Asterisk = @{"*"}
Comma = @{","}
Dot = @{"."}
Set = @{"="}
Colon = @{":"}
Vertical = @{"|"}
Plus = @{"+"}
Minus = @{"-"}
QuoteMark = @{">"}
Accent = @{"`"}
Dollar = @{"$"}
Tilde = @{"~"}