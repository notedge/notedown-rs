program = _{SOI ~ statement*  ~  EOI}
TextMode = _{SOI ~ (TextElement|TextRest)*  ~  EOI}
ListMode = {WHITE_SPACE* ~ ListMark? ~ ListText  ~ ListEnd}
statement = _{
    Header
  | HorizontalRule
  | Code
  | Table
  | List
  | Command
  | EmptyLine{1,}
  | TextBlock
}
EmptyLine = _{WHITE_SPACE*  ~  LINE_SEPARATOR}
/*====================================================================================================================*/
//!#E06C75: Header
Header = ${WHITE_SPACE* ~ Sharp{1, 6}  ~  RestOfLine}
/*====================================================================================================================*/
TextBlock = {TextHeaderCharacter  ~  TextBlockLine*}
TextHeaderCharacter = _{!LINE_SEPARATOR  ~ ANY}
TextBlockLine = _{!(EmptyLine{2,}|Sharp)  ~ ANY}
TextElement = _{StyleStatement|LineStatement|MathStatement|RawStatement|LINE_SEPARATOR|WHITE_SPACE|CommandLine|URL|Escaped}
TextRest = {(!(LINE_SEPARATOR|WHITE_SPACE|Escape|Tilde|Asterisk|Dollar|Accent) ~ ANY)+}

URL = ${ASCII_ALPHA+ ~ "://" ~ (!PATTERN_WHITE_SPACE ~ ANY)+}
/*====================================================================================================================*/
//!#61AFEF: Code
//!#E06C75: CodeText
Code = {WHITE_SPACE* ~ CodeLevel ~ CodeAction? ~SYMBOL? ~ arguments? ~WHITE_SPACE*~ CodeText ~ POP ~ CodeMark{3}}
CodeAction = {At|Asterisk|Sharp}
CodeLevel = {CodeMark{3}~PUSH(CodeMark*)}
CodeText = {(!(CodeMark ~ PEEK) ~ ANY)*}
CodeMark = @{Accent}
/*====================================================================================================================*/
//!Orange: TableMark
Table = {WHITE_SPACE* ~ TableMark ~ (!TableEnd~ANY)* ~ TableEnd}
TableEnd = {EmptyLine{2}|EmptyLine~EOI|EOI}

TableMode = _{SOI ~ TableLine*  ~  EOI}
TableLine = _{TableEscape |TableMark |TextElement| TableRest}
TableEscape = @{Escape ~ TableMark}
TableText = {(TextElement)+}
TableRest = {(!(TableMark|Escape) ~ ANY)+}
TableMark = @{Vertical}
/*====================================================================================================================*/
//!Green: ListMark
List = {WHITE_SPACE* ~ ListMark ~ ListText  ~ ListEnd}
ListText = {(!ListEnd~ANY)*}
ListEnd = {EmptyLine{2}|EmptyLine~EOI|EOI}
ListMark = @{Minus|Plus|QuoteMark|Integer~Dot}
/*====================================================================================================================*/
///Red
HorizontalRule = @{WHITE_SPACE* ~ (Minus{3,}|Plus{3,})}
/*====================================================================================================================*/
MathStatement = _{Math|MathRest}
///#E06C75
Math = {MathLevel ~ MathText ~ POP ~ Dollar}
MathLevel = {Dollar~PUSH (Dollar*)}
MathText = {(Escape~Dollar|!(Dollar ~ PEEK) ~ ANY)*}
///Gray
MathRest = {Dollar+}
/*====================================================================================================================*/
RawStatement = _{Raw|RawRest}
///#98C379
Raw = {RawLevel ~ RawText ~ POP ~ Accent}
RawLevel = {Accent~PUSH (Accent*)}
RawText = {(Escape~Accent|!(Accent ~ PEEK) ~ ANY)*}
///Gray
RawRest = {Accent+}
/*====================================================================================================================*/
StyleStatement = _{Style|StyleRest}
///#D19A66
Style = {StyleLevel ~ StyleText ~ POP ~ Asterisk}
StyleLevel = {Asterisk~PUSH (Asterisk*)}
StyleText = {(Escape~Asterisk|!(Asterisk ~ PEEK) ~ ANY)*}
///Gray
StyleRest = {Asterisk+}
/*====================================================================================================================*/
LineStatement = _{Line|LineRest}
///#61AFEF
Line = {LineLevel ~ LineText ~ POP ~ Tilde}
LineLevel = {Tilde ~ PUSH(Tilde*)}
LineText = {(Escape ~ Tilde|!(Tilde ~ PEEK) ~ ANY)*}
///Gray
LineRest = {Tilde+}
/*====================================================================================================================*/
//!#61AFEF: CommandLine|CommandBlock|CommandSection
Command = _{WHITE_SPACE* ~ (CommandLine|CommandBlock|CommandSection)}
CommandLine = ${command ~ Colon  ~  RestOfLine}
//!#56B6C2: Set
//!#E06C75: key
CommandBlock = !{command ~ (WHITE_SPACE* ~ arguments)*}
arguments = _{
    "["  ~ argument_literal ~  "]"
  | "{"  ~(PATTERN_WHITE_SPACE*~ (key_value|argument) ~PATTERN_WHITE_SPACE*)*~  "}"
}
CommandSection = !{Begin ~ (Push ~ statement+)* ~End}
argument_literal={(!"]" ~ ANY)*}
argument = {value  ~ PATTERN_WHITE_SPACE* ~  Comma?}
key_value = {key  ~PATTERN_WHITE_SPACE*~  Set  ~PATTERN_WHITE_SPACE*~ value  ~PATTERN_WHITE_SPACE*~  Comma?}
key = {Integer|String|SYMBOL}
value = {Integer|String|Keywords|SYMBOL}
//expression = {SYMBOL~(Dot~ function)+}
//function = {SYMBOL ~ ("("~PATTERN_WHITE_SPACE*~ (key_value|argument)* ~PATTERN_WHITE_SPACE*~  ")")?}

command = @{Escape ~ SYMBOL}
RestOfLine = !{(!LINE_SEPARATOR  ~  ANY)*}
Begin = @{"\\begin" ~ (WHITE_SPACE* ~ arguments)*}
End = @{"\\end" ~ (WHITE_SPACE* ~ arguments)*}
Push = @{"\\push" ~ (WHITE_SPACE* ~ arguments)*}
/*====================================================================================================================*/
//!#D19A66: Integer|Decimal|DecimalBad|Exponent|ComplexHandler
Number = ${Sign? ~ (Decimal|DecimalBad|Integer)}
Decimal = ${Integer  ~ Dot ~  ASCII_DIGIT+}
DecimalBad = ${Integer ~ Dot | Dot ~ ASCII_DIGIT+}
Integer = @{"0"|ASCII_NONZERO_DIGIT  ~  ASCII_DIGIT*}
Sign = @{Plus|Minus}
/*====================================================================================================================*/
///#98C379
String = ${
    S1  ~  (Escape ~ (Escape|S1)|!S1 ~ ANY)*  ~  S1
  | S2  ~  (Escape ~ (Escape|S2)|!S2 ~ ANY)*  ~  S2
}
S1 = _{"'"}
S2 = _{"\""}
/*====================================================================================================================*/
SYMBOL = @{
    XID_START ~ XID_CONTINUE*
  | Underline~XID_CONTINUE+
}
Escaped = @{Escape~ANY}
Keywords = @{"true"|"false"|"null"}
/*====================================================================================================================*/
//!Gray:BlockComment|LineComment
COMMENT = ${Escape ~ "comment"  ~ WHITE_SPACE* ~  Colon  ~  RestOfLine }
//LineComment      = ${Comment  ~ SPACE_SEPARATOR* ~  Colon  ~  RestOfLine}
//BlockComment     = ${Comment  ~ SPACE_SEPARATOR* ~  "{"  ~ MultiLineComment ~  "}"}
//MultiLineComment = ${(BlockComment | !"}"  ~  ANY)*}
//Comment          = @{Escape ~ "comment"}
/*====================================================================================================================*/
LINE_SEPARATOR = @{NEWLINE}
WHITE_SPACE = @{SPACE_SEPARATOR}
PATTERN_WHITE_SPACE = @{NEWLINE|SPACE_SEPARATOR|"\t"}
//TAB = @{"\t"}
//CR = @{"\r"}
//LF = @{"\n"}
/*====================================================================================================================*/
Escape = @{"\\"}
At = @{"@"}
Sharp = @{"#"}
Underline = @{"_"}
Asterisk = @{"*"}
Comma = @{","}
Dot = @{"."}
Set = @{"="}
Colon = @{":"}
Vertical = @{"|"}
Plus = @{"+"}
Minus = @{"-"}
QuoteMark = @{">"}
Accent = @{"`"}
Dollar = @{"$"}
Tilde = @{"~"}